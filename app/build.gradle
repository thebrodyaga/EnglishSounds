apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.crashlytics'
apply from: "$project.rootDir/build_scripts/module_methods.gradle"
applyJetpackCompose(this)

android {
    compileSdk libs.versions.compile.sdk.get().toInteger()
    defaultConfig {
        applicationId "com.thebrodyaga.englishsounds"
        minSdk libs.versions.min.sdk.get().toInteger()
        targetSdk libs.versions.target.sdk.get().toInteger()
        versionCode libs.versions.version.code.get().toInteger()
        versionName libs.versions.version.name.get()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        project.archivesBaseName = "English Sounds-v" + versionName + "(" + versionCode + ")"
        vectorDrawables.useSupportLibrary = true
    }
    def props = new Properties()
    file("app.properties").withInputStream { props.load(it) }
    signingConfigs {
        release {
            keyAlias props.getProperty("RELEASE_KEY_ALIAS")
            keyPassword props.getProperty("RELEASE_KEY_PASSWORD")
            storeFile file(props.getProperty("RELEASE_STORE_FILE"))
            storePassword props.getProperty("RELEASE_STORE_PASSWORD")
        }
    }

    buildTypes {
        debug {
            manifestPlaceholders = [enableCrashReporting          : "false",
                                    deactivatedAnalyticsCollection: "true",
                                    addmob_app_id                 : props.getProperty('ADMOB_APP_ID')]
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [enableCrashReporting          : "true",
                                    deactivatedAnalyticsCollection: "false",
                                    addmob_app_id                 : props.getProperty('ADMOB_APP_ID')]
        }
    }
    bundle {
        language {
            enableSplit = true
        }
        density {
            enableSplit = true
        }
        abi {
            enableSplit = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    namespace 'com.thebrodyaga.englishsounds'
}


ext {
    leakcanary = "1.5.4"
}

dependencies {

    implementation libs.bundles.base.android
    testImplementation libs.bundles.unit.test
    androidTestImplementation libs.bundles.android.test

    firebaseBom(this)
    applyDagger(this)
    applyGlide(this)
    applyPlatformNavigation(this)

    implementation libs.bundles.network
    implementation libs.google.play.review
    implementation libs.bundles.adapter.delegates
    implementation libs.bundles.google.ads
    implementation libs.timber
    implementation libs.flow.permission


    implementation project(":analytics")
    implementation project(":ad:api")
    implementation project(":ad:google")
    implementation project(":brandbook")
    implementation project(":base:di")
    implementation project(":base:app")
    implementation project(":core:utils")
    implementation project(":core:uiUtils")
    implementation project(":data:sounds:api")
    implementation project(":data:sounds:impl")
    implementation project(":data:setting:api")
    implementation project(":data:setting:impl")
    implementation project(":feature:appActivity:api")
    implementation project(":feature:appActivity:impl")
    implementation project(":feature:soundDetails:api")
    implementation project(":feature:soundDetails:impl")
    implementation project(":feature:audioPlayer:api")
    implementation project(":feature:audioPlayer:impl")
    implementation project(":feature:mainScreen:api")
    implementation project(":feature:mainScreen:impl")
    implementation project(":feature:youtube:api")
    implementation project(":feature:youtube:impl")
    implementation project(":feature:setting:api")
    implementation project(":feature:setting:impl")
    implementation project(":feature:soundList:api")
    implementation project(":feature:soundList:impl")
    implementation project(":feature:videoList:api")
    implementation project(":feature:videoList:impl")
    implementation project(":feature:training:api")
    implementation project(":feature:training:impl")
    implementation project(":legacy")


}
apply plugin: 'com.google.gms.google-services'
